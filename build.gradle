plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    repositories {
        maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://oss.sonatype.org/content/repositories/central' }
        maven { url 'https://jitpack.io' }
        mavenCentral()
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:24.1.0'
    }

    compileJava {
        options.encoding = 'UTF-8'
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
}

repositories {
    maven { url 'https://maven.enginehub.org/repo/' }
    maven { url 'https://papermc.io/repo/repository/maven-public/' }
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        include '*.yml'
        expand(rootProject.properties)
    }
}

dependencies {
    implementation project(':nms')
    implementation project(':nms:v1_7_R3')
    implementation project(':nms:v1_7_R4')
    implementation project(':nms:v1_8_R1')
    implementation project(':nms:v1_8_R2')
    implementation project(':nms:v1_8_R3')
    implementation project(':nms:v1_9_R1')
    implementation project(':nms:v1_9_R2')
    implementation project(':nms:v1_10_R1')
    implementation project(':nms:v1_11_R1')
    implementation project(':nms:v1_12_R1')

    compileOnly('org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT') {
        exclude group: 'org.yaml', module: 'snakeyaml'
    }
    compileOnly 'org.yaml:snakeyaml:2.2'

    compileOnly 'com.mojang:authlib:1.5.26'
    compileOnly('com.github.MilkBowl:VaultAPI:1.7.1') { transitive = false }
    compileOnly('com.sk89q:worldguard:6.1.1-SNAPSHOT') { transitive = false }

    compileOnly 'org.jetbrains:annotations:24.1.0'

    compileOnly fileTree(dir: 'libs', includes: ['*.jar'])
}

jar {
    enabled = false
    dependsOn (shadowJar)
}

shadowJar {
    archiveClassifier.set('')
    minimize()
}